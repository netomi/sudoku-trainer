plugins {
    id 'base'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10' apply false
}

allprojects {
    group 'org.netomi.sudoku'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects {
    ext {
        junitVersion = "5.3.2"
    }

    def srcModule = "src/main/module"
    def moduleInfo = file("${project.projectDir}/$srcModule/module-info.java")
    if (moduleInfo.exists()) {
        apply plugin: "java"
        apply plugin: "kotlin"

        sourceSets {
            module {
                java {
                    srcDirs = [srcModule]
                    compileClasspath = main.compileClasspath
                    sourceCompatibility = '11'
                    targetCompatibility = '11'
                }
            }
            main {
                kotlin { srcDirs += [srcModule] }
            }
        }

        compileModuleJava.configure {
            dependsOn compileKotlin
            destinationDir = compileKotlin.destinationDir
            doFirst {
                options.compilerArgs = ['--module-path', classpath.asPath,]
                classpath = files()
            }
        }
        jar.dependsOn compileModuleJava

        compileKotlin.finalizedBy compileModuleJava
    }
}
